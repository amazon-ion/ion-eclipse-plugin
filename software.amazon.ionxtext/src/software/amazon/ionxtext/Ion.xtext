/* Copyright 2012-2017 Amazon.com, Inc. or its affiliates. All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License").
 * You may not use this file except in compliance with the License.
 * A copy of the License is located at:
 *
 *     http://aws.amazon.com/apache2.0/
 *
 * or in the "license" file accompanying this file. This file is distributed on
 * an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either
 * express or implied. See the License for the specific language governing
 * permissions and limitations under the License.
 */

grammar software.amazon.ionxtext.Ion

hidden(WS, SL_COMMENT, ML_COMMENT)

generate ion "http://www.amazon.software/ionxtext/Ion"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore



Datagram:
    values+=Value*
;

Value:
      {Null}      annot+=Annotation* value=Null_value
    | {Bool}      annot+=Annotation* value=Bool_value
    | {Int}       annot+=Annotation* value=Int_value
    | {Float}     annot+=Annotation* value=Float_value
    | {Decimal}   annot+=Annotation* value=Decimal_value
    | {Timestamp} annot+=Annotation* value=Timestamp_value
    | {Symbol}    annot+=Annotation* value=Symbol_value
    | {String}    annot+=Annotation* value=String_value
    | {Clob}      annot+=Annotation* value=Clob_value
    | {Blob}      annot+=Annotation* value=Blob_value
    | Struct
    | List
    | Sexp
    | {BadInt}       content=BAD_INT
    | {BadFloat}     content=BAD_FLOAT
    | {BadDecimal}   content=BAD_DECIMAL
    | {BadTimestamp} content=BAD_TIMESTAMP
    | {BadNulltype}  content=BAD_NULLTYPE
;

Null_value:
    NULL
;

Bool_value:
    (BOOL | BOOL_NULL)
;

Int_value:
    (BIN_INTEGER | DEC_INTEGER | HEX_INTEGER | INT_NULL)
;

Float_value:
    (FLOAT | SPECIAL_FLOAT | FLOAT_NULL)
;

Decimal_value:
    (DECIMAL | DECIMAL_NULL)
;

Timestamp_value:
    (TIMESTAMP | TIMESTAMP_NULL)
;

Symbol_value:
    (IDENTIFIER_SYMBOL | QUOTED_SYMBOL | SYMBOL_NULL)
;

String_value:
    (DOUBLEQUOTEDSTRING | TRIPLEQUOTEDSTRING+ | STRING_NULL)
;

Clob_value:
    (CLOB | CLOB_NULL)
;

Blob_value:
    (BLOB | BLOB_NULL)
;

Sexp:
    {Sexp} annot+=Annotation* ( '(' items+=Sexp_item* ')' | SEXP_NULL )
;

List:
    {List} annot+=Annotation* ( ('[' (items+=Value (',' items+=Value)* ','?)? ']') | LIST_NULL )
;

Struct:
    {Struct} annot+=Annotation* ( ('{' (items+=Field (',' items+=Field)* ','?)? '}') | STRUCT_NULL )
;

Sexp_item:
    Value | Unquoted_Operator
;

Unquoted_Operator:
    {Unquoted_Operator} annot+=Annotation* value=OP
;

Annotation:
    value=(IDENTIFIER_SYMBOL | QUOTED_SYMBOL) '::'
;

Field:
    fieldName=Field_name ':' value=Value
;

Field_name:
    IDENTIFIER_SYMBOL
    | QUOTED_SYMBOL
    | DOUBLEQUOTEDSTRING
//    | SHORT_QUOTED_STRING
    | TRIPLEQUOTEDSTRING+
//    | LONG_QUOTED_STRING+  // TODO spec is (WS* LQS)+
;

// ========================== TERMINAL RULES ===========================


terminal
BOOL_NULL:
    'null.bool'
;

terminal
INT_NULL:
    'null.int'
;

terminal
FLOAT_NULL:
    'null.float'
;

terminal
DECIMAL_NULL:
    'null.decimal'
;

terminal
TIMESTAMP_NULL:
    'null.timestamp'
;

terminal
SYMBOL_NULL:
    'null.symbol'
;

terminal
STRING_NULL:
    'null.string'
;

terminal
CLOB_NULL:
    'null.clob'
;

terminal
BLOB_NULL:
    'null.blob'
;

terminal
SEXP_NULL:
    'null.sexp'
;

terminal
LIST_NULL:
    'null.list'
;

terminal
STRUCT_NULL:
    'null.struct'
;

terminal
NULL:
    'null' | 'null.null'
;

//////////////////////////////////////////////////////////////////////////////
// Ion Bool
//////////////////////////////////////////////////////////////////////////////

terminal
BOOL
    : 'true'
    | 'false'
    ;


//////////////////////////////////////////////////////////////////////////////
// Ion Timestamp
//////////////////////////////////////////////////////////////////////////////

terminal
TIMESTAMP
    : DATE ('T' TIME?)?
    | YEAR '-' MONTH 'T'
    | YEAR 'T'
    ;

terminal
fragment
DATE
    : YEAR '-' MONTH '-' DAY
    ;

terminal
fragment
YEAR
    : '000'                            ('1'..'9')
    | '00'                  ('1'..'9') DEC_DIGIT
    | '0'        ('1'..'9') DEC_DIGIT  DEC_DIGIT
    | ('1'..'9') DEC_DIGIT  DEC_DIGIT  DEC_DIGIT
    ;

terminal
fragment
MONTH
    : '0' ('1'..'9')
    | '1' ('0'..'2')
    ;

terminal
fragment
DAY
    : '0'        ('1'..'9')
    | ('1'..'2') DEC_DIGIT
    | '3'        ('0'..'1')
    ;

terminal
fragment
TIME
    : HOUR ':' MINUTE (':' SECOND)? OFFSET
    ;

terminal
fragment
OFFSET
    : 'Z'
    | PLUS_OR_MINUS HOUR ':' MINUTE
    ;

terminal
fragment
HOUR
    : ('0'..'1') DEC_DIGIT
    | '2'        ('0'..'3')
    ;

terminal
fragment
MINUTE
    : ('0'..'5') DEC_DIGIT
    ;

// note that W3C spec requires a digit after the '.'
terminal
fragment
SECOND
    : ('0'..'5') DEC_DIGIT ('.' DEC_DIGIT+)?
    ;


//////////////////////////////////////////////////////////////////////////////
// Ion Int
//////////////////////////////////////////////////////////////////////////////

terminal
BIN_INTEGER
    : '-'? '0' ('b' | 'B') BINARY_DIGIT (UNDERSCORE? BINARY_DIGIT)*
    ;

terminal
DEC_INTEGER
    : '-'? DEC_UNSIGNED_INTEGER
    ;

terminal
HEX_INTEGER
    : '-'? '0' ('x' | 'X') HEX_DIGIT (UNDERSCORE? HEX_DIGIT)*
    ;


//////////////////////////////////////////////////////////////////////////////
// Ion Float
//////////////////////////////////////////////////////////////////////////////

terminal
SPECIAL_FLOAT
    : PLUS_OR_MINUS 'inf'
    | 'nan'
    ;

terminal
FLOAT
    : DEC_INTEGER DEC_FRAC? FLOAT_EXP
    ;

terminal
fragment
FLOAT_EXP
    : ('E' | 'e') PLUS_OR_MINUS? DEC_DIGIT+
    ;


//////////////////////////////////////////////////////////////////////////////
// Ion Decimal
//////////////////////////////////////////////////////////////////////////////

terminal
DECIMAL
    : DEC_INTEGER DEC_FRAC? DECIMAL_EXP?
    ;

terminal
fragment
DECIMAL_EXP
    : ('D' | 'd') PLUS_OR_MINUS? DEC_DIGIT+
    ;


//////////////////////////////////////////////////////////////////////////////
// Ion Symbol
//////////////////////////////////////////////////////////////////////////////

terminal
QUOTED_SYMBOL
    : SYMBOL_QUOTE ( TEXT_ESCAPE  | SYMBOL_TEXT_ALLOWED )+ SYMBOL_QUOTE
    ;

// FIXME: Allow U+007F U+00FE U+00FF
terminal fragment
SYMBOL_TEXT_ALLOWED:
      //'\u0008'                      // tab - causes error in Xtext
    '\t'
    | '\u0020'..'\u0026' // no C1 control characters and no U+0027 single quote
    | '\u0028'..'\u005B' // no U+005C backslash
    | '\u005D'..'\u007e'              // skip '\u007f' del
    | '\u0080'..'\ufffd'              // rest of the 16 bit code points
    // skip '\uffff' - causes error in Xtext
    ;

terminal //fragment
IDENTIFIER_SYMBOL
    : ( '$' | '_' | 'A'..'Z' | 'a'..'z' )  ( '$' | '_' | 'A'..'Z' | 'a'..'z' | DEC_DIGIT )*
    ;

terminal
OP:
    OPERATOR+
;


//////////////////////////////////////////////////////////////////////////////
// Ion String
//////////////////////////////////////////////////////////////////////////////

terminal
DOUBLEQUOTEDSTRING:
    SHORT_QUOTED_STRING // TODO inline this, but it breaks tests.
;

terminal
SHORT_QUOTED_STRING
    : SHORT_QUOTE STRING_SHORT_TEXT SHORT_QUOTE
    ;

terminal
TRIPLEQUOTEDSTRING:
    LONG_QUOTE
    ( TEXT_ESCAPE | TRIPLEQUOTEDCHAR | JUST_ONEQUOTE | JUST_TWOQUOTE )*
    LONG_QUOTE
;


terminal fragment
STRING_SHORT_TEXT
    : (TEXT_ESCAPE | STRING_SHORT_TEXT_ALLOWED)*
    ;

// TODO Official grammar is non-greedy (*?) at STRING_LONG_TEXT

// non-control Unicode and not double quote or backslash
terminal fragment
STRING_SHORT_TEXT_ALLOWED
    // TODO old grammar has '\t' U+0008
    : '\u0020'..'\u0021' // no C1 control characters and no U+0022 double quote
    | '\u0023'..'\u005B' // no U+005C backslash
    | '\u005D'..'\uFFFF' // FIXME should be up to U+10FFFF
    ;

// non-control Unicode (newlines are OK)
terminal fragment
STRING_LONG_TEXT_ALLOWED
    : '\u0020'..'\u005B' // no C1 control characters and no U+005C blackslash
    | '\u005D'..'\uFFFF' // FIXME should be up to U+10FFFF
    | NL
    ;

terminal fragment
TRIPLEQUOTEDCHAR:
    //'\u0008'                      // tab - causes error in Xtext
      '\t'
    | '\u000A'                      // new line
    | '\u000D'                      // carriage return
    | '\u0020'..'\u0026'            // space through andpersand
    //  we don't skip the singe quote - but we recognize it distinctly
    | '\u0028'..'\u005B'            // open paren through close brace
    // skip '\u005C' backslash
    | '\u005D'..'\u007e'            // open brace through tilde, end of ASCII
    // skip '\u007f' del
    | '\u0080'..'\ufffd'            // rest of the 16 bit code points
;

terminal fragment
JUST_ONEQUOTE:
    "'" (TEXT_ESCAPE | TRIPLEQUOTEDCHAR)
;

terminal fragment
JUST_TWOQUOTE:
    "''" (TEXT_ESCAPE | TRIPLEQUOTEDCHAR)
;

terminal
fragment
TEXT_ESCAPE
    : COMMON_ESCAPE | HEX_ESCAPE | UNICODE_ESCAPE
    ;

//////////////////////////////////////////////////////////////////////////////
// Ion CLOB
//////////////////////////////////////////////////////////////////////////////

terminal
CLOB
    : SHORT_QUOTED_CLOB
    | LONG_QUOTED_CLOB
    ;

terminal
SHORT_QUOTED_CLOB
    : LOB_START WS* SHORT_QUOTE CLOB_SHORT_TEXT SHORT_QUOTE WS* LOB_END
    ;

// TODO Official grammar is non-greedy (*?) at CLOB_LONG_TEXT

terminal
LONG_QUOTED_CLOB
    : LOB_START (WS* LONG_QUOTE CLOB_LONG_TEXT* LONG_QUOTE)+ WS* LOB_END
    ;

terminal fragment
CLOB_SHORT_TEXT
    : (CLOB_ESCAPE | CLOB_SHORT_TEXT_ALLOWED)*
    ;

terminal fragment
CLOB_LONG_TEXT
    : CLOB_LONG_TEXT_NO_QUOTE
    | '\'' CLOB_LONG_TEXT_NO_QUOTE
    | '\'\'' CLOB_LONG_TEXT_NO_QUOTE
    ;

terminal fragment
CLOB_LONG_TEXT_NO_QUOTE
    : (CLOB_ESCAPE | CLOB_LONG_TEXT_ALLOWED)
    ;

// FIXME Old grammar accepted U+007F but new one does not. Missing test?

// non-control ASCII and not double quote or backslash
terminal fragment
CLOB_SHORT_TEXT_ALLOWED
    : '\u0020'..'\u0021' // no U+0022 double quote
    | '\u0023'..'\u005B' // no U+005C backslash
    | '\u005D'..'\u007E'
    ;

// non-control ASCII (newlines are OK)
terminal fragment
CLOB_LONG_TEXT_ALLOWED
    : '\u0020'..'\u0026' // no U+0027 single quote
    | '\u0028'..'\u005B' // no U+005C blackslash
    | '\u005D'..'\u007E'
    | NL
    ;

terminal
fragment
CLOB_ESCAPE
    : COMMON_ESCAPE | HEX_ESCAPE
    ;


//////////////////////////////////////////////////////////////////////////////
// Ion BLOB
//////////////////////////////////////////////////////////////////////////////

terminal
BLOB
    : LOB_START WS* BASE_64_QUARTET* BASE_64_PAD? LOB_END
    ;

terminal fragment
BASE_64_PAD
    : BASE_64_PAD1
    | BASE_64_PAD2
    ;

terminal fragment
BASE_64_QUARTET
    : BASE_64_CHAR WS* BASE_64_CHAR WS* BASE_64_CHAR WS* BASE_64_CHAR WS*
    ;

terminal fragment
BASE_64_PAD1
    : BASE_64_CHAR WS* BASE_64_CHAR WS* BASE_64_CHAR WS* '=' WS*
    ;

terminal fragment
BASE_64_PAD2
    : BASE_64_CHAR WS* BASE_64_CHAR WS* '=' WS* '=' WS*
    ;

terminal fragment
BASE_64_CHAR
    : '0'..'9'
    | 'a'..'z'
    | 'A'..'Z'
    | '+'
    | '/'
    ;


//////////////////////////////////////////////////////////////////////////////
// Common Lexical Primitives
//////////////////////////////////////////////////////////////////////////////

terminal fragment
LOB_START
    : '{{'
    ;

terminal fragment
LOB_END
    : '}}'
    ;

terminal
fragment SYMBOL_QUOTE : '\'';

terminal fragment
SHORT_QUOTE
    : '"'
    ;

terminal fragment
LONG_QUOTE
    : "'''"
    ;

terminal fragment
DEC_UNSIGNED_INTEGER :
    '0'
  | '1'..'9' (UNDERSCORE? DEC_DIGIT)*
;

terminal fragment
DEC_FRAC
    : '.'
    | '.' DEC_DIGIT (UNDERSCORE? DEC_DIGIT)*
    ;

terminal fragment
DEC_DIGIT
    : '0'..'9'
    ;

terminal
fragment
HEX_DIGIT
    : '0'..'9'
    | 'a'..'f'
    | 'A'..'F'
    ;

terminal
fragment
BINARY_DIGIT
    : ( '0' | '1' )
    ;

terminal fragment
PLUS_OR_MINUS
    : ( '+' | '-' )
    ;

terminal fragment
OPERATOR:
     '!' // = 33
  |  '#' // = 35  TODO: this is not included in the C parser
  |  '%' // = 37
  |  '&' // = 38
  |  '*' // = 42
  |  '+' // = 43
  |  '-' // = 45
  |  '.' // = 46
  |  '/' // = 47
  |  ';' // = 59
  |  '<' // = 60
  |  '=' // = 61
  |  '>' // = 62
  |  '?' // = 63
  |  '@' // = 64
  |  '^' // = 94
  |  '`' // = 96
  |  '|' // =124
  |  '~' // =126
;

terminal
fragment
COMMON_ESCAPE
    : '\\' COMMON_ESCAPE_CODE
    ;

terminal
fragment
COMMON_ESCAPE_CODE
    : 'a'
    | 'b'
    | 't'
    | 'n'
    | 'f'
    | 'r'
    | 'v'
    | '?'
    | '0'
    | '\''
    | '"'
    | '/'
    | '\\'
    | NL
    ;

terminal
fragment
HEX_ESCAPE
    : '\\' 'x' HEX_DIGIT HEX_DIGIT
    ;

// Note: must separate backslash and u to workaround Xtext bug.
terminal
fragment
UNICODE_ESCAPE
    : '\\' 'u'     HEX_DIGIT_QUARTET
    | '\\' 'U000'  HEX_DIGIT_QUARTET HEX_DIGIT
    | '\\' 'U0010' HEX_DIGIT_QUARTET
    ;

terminal
fragment
HEX_DIGIT_QUARTET
    : HEX_DIGIT HEX_DIGIT HEX_DIGIT HEX_DIGIT
    ;

terminal
WS
  :  '\t'
  |  '\n'
//|  '\u000B' // FIXME vertical tab
//|  '\u000C' // FIXME form feed -- causes error in xtext
  |  '\r'
  |  ' '
  |  '\\' ('\r')? '\n'
;

terminal fragment
NL
    : '\u000D\u000A'  // carriage return + line feed
    | '\u000D'        // carriage return
    | '\u000A'        // line feed
    ;

terminal
ML_COMMENT:
    '/*' -> '*/'
;

terminal
SL_COMMENT:
    '//' !('\n'|'\r')* ('\r'? '\n')?
;


terminal fragment
UNDERSCORE
    : '_'
    ;


//////////////////////////////////////////////////////////////////////////////
// Erroneous tokens
//////////////////////////////////////////////////////////////////////////////

// WARNING!
//
// These terminals must stay near the end of the file, so they do not take
// precedence (in the lexer) over well-formed terminals.

terminal
BAD_NULLTYPE:
    'null.' (IDENTIFIER_SYMBOL)
;

// TODO Trap invalid leading zeroes, eg 0123
terminal
BAD_INT:
    DEC_INTEGER (IDENTIFIER_SYMBOL | DEC_INTEGER | '/')
;

terminal
BAD_FLOAT:
    FLOAT (IDENTIFIER_SYMBOL | DEC_INTEGER | '/')
;

terminal
BAD_DECIMAL:
    DECIMAL (IDENTIFIER_SYMBOL | DEC_INTEGER | '/')
;

terminal
BAD_TIMESTAMP:
    TIMESTAMP (IDENTIFIER_SYMBOL | DEC_INTEGER | '/')
;
